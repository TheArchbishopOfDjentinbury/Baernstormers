PREFIX sc:   <https://static.rwpz.net/spendcast/schema#>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>

SELECT ?receipt ?date ?merchant ?merchantLabel ?merchantCategory
       (SUM(xsd:decimal(?lineSubtotal)) AS ?receiptTotal)
       (SAMPLE(?desc) AS ?receiptDescription)
WHERE {
  ?receipt rdf:type sc:Receipt ;
           sc:receiptDate ?date ;
           sc:merchant ?merchant ;
           sc:hasLineItem ?lineItem .

  ?merchant rdf:type sc:Merchant ;
            rdfs:label ?merchantLabel ;
            sc:merchantCategory ?merchantCategory .

  # Transport MCCs
  FILTER(
    ?merchantCategory = <https://static.rwpz.net/spendcast/mcc/MCC_3015> ||  # Airlines
    ?merchantCategory = <https://static.rwpz.net/spendcast/mcc/MCC_4111> ||  # Local public transport
    ?merchantCategory = <https://static.rwpz.net/spendcast/mcc/MCC_4112>     # Passenger railway
  )

  ?lineItem sc:lineSubtotal ?lineSubtotal .

  OPTIONAL { ?receipt sc:description ?desc }
}
GROUP BY ?receipt ?date ?merchant ?merchantLabel ?merchantCategory
ORDER BY ?date ?merchantLabel
